plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'signing'
    id 'maven-publish'
}

def VERSION = "4.7.0"
def GROUP_ID = "io.github.neutronstarer"
def ARTIFACT_ID = "npc"
def DESCRIPTION = "Near Procedure Call"

android {
    namespace 'com.neutronstarer.npc'
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

afterEvaluate{
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    publishing {
        repositories {
            maven {
                def releaseRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = VERSION.endsWith('SNAPSHOT') ? snapshotRepoUrl : releaseRepoUrl
                credentials {
                    username properties.get("ossrh.username")
                    password properties.get("ossrh.password")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId GROUP_ID
                artifactId ARTIFACT_ID
                version VERSION
                pom {
                    packaging = 'jar'
                    name = ARTIFACT_ID
                    description = DESCRIPTION
                    version VERSION
                    url = "https://github.com/neutronstarer/npc_kotlin"
                    licenses {
                        license {
                            name = "BSD 3-Clause"
                            url = "https://github.com/neutronstarer/npc_kotlin/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = 'neutronstarer'
                            name = 'neutronstarer'
                            email = 'neutronstarer@gmail.com'
                        }
                    }
                    scm {
                        url = "https://github.com/neutronstarer/npc_kotlin"
                        connection = "scm:git:git@github.com:neutronstarer/npc_kotlin.git"
                        developerConnection = "scm:git:git@github.com:neutronstarer/npc_kotlin.git"
                    }
                }
            }
        }

    }

    signing {
        sign publishing.publications.release
    }
}
